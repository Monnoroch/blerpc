task runFindbugs

subprojects {
    // Reactive-blerpc-test project doesn't have java files in 'src' dir. When findbugs can't find
    // java files for checking, then it throws an error.
    if (project.getName().contentEquals('reactive-blerpc-test')) {
        return
    }
    def isAndroidLibrary = project.getPlugins().hasPlugin('com.android.library')

    apply plugin: 'findbugs'

    task customFindbugs(type: FindBugs) {
        ignoreFailures = true
        effort = "max"
        reportLevel = "medium"
        if (isAndroidLibrary) {
            classes = files("${project.buildDir}/intermediates/classes")
        } else {
            classes = files("${project.buildDir}/classes")
        }
        excludeFilter = file("${rootProject.projectDir}/linters/configs/findbugs.xml")
        source = fileTree('src')
        classpath = files()
        reports {
            xml.enabled = true
            xml.destination "${rootProject.buildDir}/linters/${project.name}.findbugs.xml"
        }
    }
    runFindbugs.dependsOn("${project.path}:customFindbugs")
}
